class Kid2KidTest
types
	public NotEmptyString = seq1 of char;
instance variables
	kid2kid : Kid2Kid := new Kid2Kid();
	client: Client := new Client("abc");
	store: Store := new Store("Porto");
	cashier: StoreCashier;
	today: Date := new Date(4,1,2019);
operations
	
	private assertTrue: bool ==> ()
	 assertTrue(cond) == return
	 pre cond;
	 
	private loginAdmin: () ==> ()
	loginAdmin() ==
	(
		assertTrue(kid2kid.login("Admin") = <Admin>)
	);
	
	private loginCashier: NotEmptyString ==> ()
	loginCashier(cashierName) ==
	(
		assertTrue(kid2kid.login(cashierName) = <Cashier>)
	);
	 
	private testAddClient: () ==> ()
		testAddClient() ==
		(
			assertTrue(card kid2kid.getClients() = 0);
			kid2kid.addClient(client);
			assertTrue(card kid2kid.getClients() = 1);
		);
			
	private testAddStore: () ==> ()
		testAddStore() ==
		(
			assertTrue(card kid2kid.getStores() = 0);
			kid2kid.addStore(store);
			assertTrue(card kid2kid.getStores() = 1);
		);
		
	private testAddCashierToStore: (NotEmptyString) ==> ()
		testAddCashierToStore(name) ==
		(
			cashier := new StoreCashier(name, store);
			store.addCashier(cashier);
			assertTrue(card store.getCashiers() = 1);
		);
		
	private testStoreBuyProductInCash : () ==> ()
		testStoreBuyProductInCash() ==
		(
			dcl p: Product := new Toy(<New>, 5, <Cars>);
			assertTrue(p not in set store.getProductsAvailable());
			assertTrue(p not in set client.getProductsSold());
			kid2kid.buyProductInCash(p,client,cashier,today);
			assertTrue(p in set store.getProductsAvailable());
			assertTrue(p in set client.getProductsSold());
		);
		
	private testStoreBuyProductInCreditNotes: () ==> ()
		testStoreBuyProductInCreditNotes() ==
		(
			dcl p: Product := new Toy(<Low_Use>, 2, <Puzzles>);
			assertTrue(p not in set store.getProductsAvailable());
			assertTrue(p not in set client.getProductsSold());
			kid2kid.buyProductInCreditNotes(p, client, cashier, today);
			assertTrue(p in set store.getProductsAvailable());
			assertTrue(p in set client.getProductsSold());
			assertTrue(store.getCreditNotesOfClient(client.getId()) > 0);
		);
	
	public testAdminOperations: () ==> ()
		testAdminOperations() == 
			(
			loginAdmin();
			testAddClient();
			testAddStore();
			testAddCashierToStore("Joao");
			);
			
	public testCashierOperations: () ==> ()
		testCashierOperations() ==
			(
			loginCashier("Joao");
			testStoreBuyProductInCash();
			testStoreBuyProductInCreditNotes();
			);
	
	public static main: () ==> ()
		main() ==
		(
			dcl kid2KidTest: Kid2KidTest := new Kid2KidTest();
			kid2KidTest.testAdminOperations();
			kid2KidTest.testCashierOperations();
		);

traces
-- TODO Define Combinatorial Test Traces here
end Kid2KidTest