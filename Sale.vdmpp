-- Represents a collection of items that were sold by a store
class Sale is subclass of Transaction
instance variables
	giftCardsUsed: set of GiftCard := {};
	giftCardsSold: set of GiftCard := {};
	
operations

	-- Store sells products. Client possibly uses active gift cards.
	public Sale: Date * Client * set of Product * set of GiftCard * StoreCashier ==> Sale
	Sale(d, c, p, cardsUsed, sc) ==
		(
		date:=d;
		client:=c;
		productsTransacted:=p;
		giftCardsUsed:=cardsUsed;
		storeAuthorizer:= sc;
		value:=sumProductValues(p);
		return self
		)
		pre card p >= 1 -- there is at least on product being sold
		post 
			date = d and 
			client = c and 
			productsTransacted = p and 
			giftCardsUsed = cardsUsed and 
			storeAuthorizer = sc and 
			value = sumProductValues(productsTransacted) and
			RESULT = self;
		
	-- Store sells gift cards.
	public Sale: Date * Client * set of GiftCard * StoreCashier ==> Sale
	Sale(d, c, g, sc) ==
		(
		date := d;
		client := c;
		giftCardsSold := g;
		storeAuthorizer := sc;
		value := sumGCValues(g);
		return self;
		)
		pre card g >= 1
		post value = sumGCValues(g) and 
		date = d and 
		client = c and 
		giftCardsSold = g and 
		storeAuthorizer = sc and 
		RESULT = self;
	
	-- Get gift cards that were used in the transaction
	public pure getGiftCardsUsed: () ==> set of GiftCard
	getGiftCardsUsed() ==
		return giftCardsUsed
		pre true
		post RESULT = giftCardsUsed;
		
	-- Get gift cards that were sold in the transaction
	public pure getGiftCardsSold: () ==> set of GiftCard
	getGiftCardsSold() ==
		return giftCardsSold
		pre true
		post RESULT = giftCardsSold;
	
	
	-- Sum the sell prices of the products	
	protected pure sumProductValues: set of Product ==> real
	sumProductValues(pSet) ==
		(
		dcl result: real := 0;
		for all p in set pSet do 
			(
			result := result + p.getSellPrice();
			);
		return result;
		)
		pre true
		post true; -- same as body
	
	-- Sum the values of a set of giftcards
	private pure sumGCValues: set of GiftCard ==> nat
	sumGCValues(gcSet) ==
		(
		dcl result: nat := 0;
		for all g in set gcSet do 
			(
			result := result + g.getValue();
			);
		return result;
		)
		pre true
		post true; -- same as body

end Sale